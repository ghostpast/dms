///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СтандартныеПодсистемы

Функция СП_ПараметрыРаботыКлиентаПриЗапуске() Экспорт
	СП_ПроверитьПорядокЗапускаПрограммыПередНачаломРаботыСистемы();

	ПараметрыПриЗапускеПрограммы = ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыПриЗапускеПрограммы"];

	Параметры = Новый Структура;
	Параметры.Вставить("ПолученныеПараметрыКлиента", Неопределено);

	Если ПараметрыПриЗапускеПрограммы.Свойство("ПолученныеПараметрыКлиента") И ТипЗнч(ПараметрыПриЗапускеПрограммы.ПолученныеПараметрыКлиента) = Тип("Структура") Тогда
		Параметры.Вставить("ПолученныеПараметрыКлиента", БазоваяПодсистемаКлиент.ОН_СкопироватьРекурсивно(ПараметрыПриЗапускеПрограммы.ПолученныеПараметрыКлиента));
	КонецЕсли;

	Если ПараметрыПриЗапускеПрограммы.Свойство("ПропуститьОчисткуСкрытияРабочегоСтола") Тогда
		Параметры.Вставить("ПропуститьОчисткуСкрытияРабочегоСтола");
	КонецЕсли;

	Если ПараметрыПриЗапускеПрограммы.Свойство("ОпцииИнтерфейса") И ТипЗнч(Параметры.ПолученныеПараметрыКлиента) = Тип("Структура") Тогда
		Параметры.ПолученныеПараметрыКлиента.Вставить("ОпцииИнтерфейса");
	КонецЕсли;

	БазоваяПодсистемаКлиент.СП_ЗаполнитьПараметрыКлиента(Параметры);

	ПараметрыКлиента = БазоваяПодсистемаВызовСервера.СП_ПараметрыРаботыКлиентаПриЗапуске(Параметры);

	Если ПараметрыПриЗапускеПрограммы.Свойство("ПолученныеПараметрыКлиента")
		И ПараметрыПриЗапускеПрограммы.ПолученныеПараметрыКлиента <> Неопределено
		И Не ПараметрыПриЗапускеПрограммы.Свойство("ОпцииИнтерфейса") Тогда

		ПараметрыПриЗапускеПрограммы.Вставить("ОпцииИнтерфейса", ПараметрыКлиента.ОпцииИнтерфейса);
	КонецЕсли;

	БазоваяПодсистемаКлиент.СП_ЗаполнитьПараметрыКлиента(ПараметрыКлиента);

	// Обновление состояния скрытия рабочего стола на клиенте по состоянию на сервере.
	БазоваяПодсистемаКлиент.СП_СкрытьРабочийСтолПриНачалеРаботыСистемы(Параметры.СкрытьРабочийСтолПриНачалеРаботыСистемы, Истина);

	Возврат ПараметрыКлиента;
КонецФункции

Функция СП_ПараметрыРаботыКлиента() Экспорт
	СП_ПроверитьПорядокЗапускаПрограммыПередНачаломРаботыСистемы();
	СП_ПроверитьПорядокЗапускаПрограммыПриНачалеРаботыСистемы();

	СвойстваКлиента = Новый Структура;
	СвойстваКлиента.Вставить("ПараметрЗапуска",							ПараметрЗапуска);
	СвойстваКлиента.Вставить("СтрокаСоединенияИнформационнойБазы",		СтрокаСоединенияИнформационнойБазы());
	СвойстваКлиента.Вставить("ЭтоВебКлиент",							БазоваяПодсистемаКлиент.СП_ЭтоВебКлиент());
	СвойстваКлиента.Вставить("ЭтоLinuxКлиент",							БазоваяПодсистемаКлиент.ОН_ЭтоLinuxКлиент());
	СвойстваКлиента.Вставить("ЭтоMacOSКлиент",							БазоваяПодсистемаКлиент.ОН_ЭтоMacOSКлиент());
	СвойстваКлиента.Вставить("ЭтоWindowsКлиент",						БазоваяПодсистемаКлиент.ОН_ЭтоWindowsКлиент());
	СвойстваКлиента.Вставить("ЭтоМобильныйКлиент",						БазоваяПодсистемаКлиент.СП_ЭтоМобильныйКлиент());
	СвойстваКлиента.Вставить("ИспользуемыйКлиент",						БазоваяПодсистемаКлиент.СП_ИспользуемыйКлиент());
	СвойстваКлиента.Вставить("КаталогПрограммы",						БазоваяПодсистемаКлиент.СП_ТекущийКаталогПрограммы());
	СвойстваКлиента.Вставить("ИдентификаторКлиента",					БазоваяПодсистемаКлиент.СП_ИдентификаторКлиента());
	СвойстваКлиента.Вставить("СкрытьРабочийСтолПриНачалеРаботыСистемы",	Ложь);
	СвойстваКлиента.Вставить("ОперативнаяПамять",						БазоваяПодсистемаКлиент.ОН_ОперативнаяПамятьДоступнаяКлиентскомуПриложению());
	СвойстваКлиента.Вставить("РазрешениеОсновногоЭкрана",				БазоваяПодсистемаКлиент.СП_РазрешениеОсновногоЭкрана());

	// Установка даты клиента непосредственно перед вызовом, чтобы уменьшить погрешность.
	// АПК:143-выкл - №643.2.1 Требуется ТекущаяДата для расчета ПоправкаКВремениСеанса.
	СвойстваКлиента.Вставить("ТекущаяДатаНаКлиенте", ТекущаяДата());
	// АПК:143-вкл.
	СвойстваКлиента.Вставить("ТекущаяУниверсальнаяДатаВМиллисекундахНаКлиенте", ТекущаяУниверсальнаяДатаВМиллисекундах());

	Возврат БазоваяПодсистемаВызовСервера.СП_ПараметрыРаботыКлиента(СвойстваКлиента);
КонецФункции

Процедура СП_ПроверитьПорядокЗапускаПрограммыПередНачаломРаботыСистемы()
	ИмяПараметра = "СтандартныеПодсистемы.ЗапускПрограммыЗавершен";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ВызватьИсключение
			"Ошибка порядка запуска программы.
				|Первой процедурой, которая вызывается из обработчика события ПередНачаломРаботыСистемы
				|должна быть процедура БазоваяПодсистемаКлиент.ПередНачаломРаботыСистемы.";
	КонецЕсли;
КонецПроцедуры

Процедура СП_ПроверитьПорядокЗапускаПрограммыПриНачалеРаботыСистемы()
	Если Не БазоваяПодсистемаКлиент.СП_ЗапускПрограммыЗавершен() Тогда
		ВызватьИсключение
			"Ошибка порядка запуска программы.
				|Перед получением параметров работы клиента запуск программы должен быть завершен.";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

